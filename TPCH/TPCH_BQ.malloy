-- source data at https://drive.google.com/drive/folders/1mZC3NuPBZC4mjP3_kH18c9fLrv8ME7RU
-- Malloy file require a Visual studio extension  https://marketplace.visualstudio.com/items?itemName=malloydata.malloy-vscode
-- Lineitem parquet file contains 59 Million records, you can just duplicate the file to increase the data size for testing.
source:lineitem is table('test-187010.TPCH.lineitem')
{ 

  join_one: orders is table('test-187010.TPCH.orders')
                    on orders.O_ORDERKEY = L_ORDERKEY
  join_one: customer is table('test-187010.TPCH.customer')
                     on customer.C_CUSTKEY = orders.O_CUSTKEY
  join_one: nation is table('test-187010.TPCH.nation')
                    on nation.N_NATIONKEY = customer.C_NATIONKEY
  join_one: region is table('test-187010.TPCH.region')
                    on  region.R_REGIONKEY =nation.N_REGIONKEY
  join_one: partsupp is table('test-187010.TPCH.partsupp')
                    on partsupp.PS_PARTKEY = L_PARTKEY and partsupp.PS_SUPPKEY = L_SUPPKEY
  join_one: supplier is table('test-187010.TPCH.supplier')
                    on supplier.S_SUPPKEY = partsupp.PS_SUPPKEY 
  join_one: part is table('test-187010.TPCH.part')
                    on part.P_PARTKEY = partsupp.PS_PARTKEY
  join_one: nation_supplier is table('test-187010.TPCH.nation')
                    on nation_supplier.N_NATIONKEY = supplier.S_NATIONKEY
  join_one: region_supplier is table('test-187010.TPCH.region')
                    on  region_supplier.R_REGIONKEY =nation_supplier.N_REGIONKEY
  
  
  dimension : Shipdate_year is L_SHIPDATE.year
              

  measure :   _count is count()
              _count_all is all(_count)
              _Ratio is _count/_count_all
              sum_qty is SUM(L_QUANTITY) 
              sum_base_price is SUM(L_EXTENDEDPRICE)
              sum_disc_price is SUM(L_EXTENDEDPRICE * (1 - L_DISCOUNT)) 
              sum_charge is SUM(L_EXTENDEDPRICE * (1 - L_DISCOUNT) * (1 + L_TAX)) 
              avg_qty is AVG(L_QUANTITY) 
              avg_price is AVG(L_EXTENDEDPRICE) 
              avg_disc is AVG(L_DISCOUNT) 


Query : by_year is {
    group_by : Shipdate_year
    aggregate :Sold_BY_Year is sum_qty/all(sum_qty)
                   }

Query: TPCH1 is {
    
    group_by : L_RETURNFLAG,
               L_LINESTATUS
    aggregate: sum_qty,sum_base_price,sum_disc_price,sum_charge,
               avg_qty,avg_price,avg_disc,_count,_count_all
                 }
Query : by_flag is {
    group_by : L_RETURNFLAG
    aggregate :_count_all,_count,_Ratio
                   }

Query : by_shipmod_flag is {
    aggregate :_count_all
          nest : xxx is {
              group_by: region.R_NAME
              nest : yyy is {
                    group_by :nation.N_NAME
                        nest: by_flag
                             }

                        }
  
                            }

Query : by_Country_Country_supplier is {
    group_by : Supplier_Country is nation_supplier.N_NAME
    aggregate :sum_qty
    limit : 30
                   }


}